from "micro.ato" import Micro
from "power-stage.ato" import PowerStage
from "power-supply.ato" import PowerSupply
from "position-sensor.ato" import PositionSensor
from "ios.ato" import IOs

# TODO: fix buck power supply, select appropriate inductor and diode manually
# TODO: refactor STM32G4
# TODO: voltage sense
# TODO: temperature sense
# TODO: CAN transceiver

module SPIN:
    """
    Brushless motor controller with current sensing and position feedback.
    """
    power_stage = new PowerStage
    power_supply = new PowerSupply
    micro = new Micro
    position_sensor = new PositionSensor
    ios = new IOs

    # Connect the components
    micro.power ~ power_supply.power3v3
    position_sensor.power ~ power_supply.power3v3
    ios.power5v ~ power_supply.power5v #Power for LEDs
    ios.power3v3 ~ power_supply.power3v3

    # Phase control
    power_stage.phase_a.control ~ micro.phase_a_control
    power_stage.phase_b.control ~ micro.phase_b_control
    power_stage.phase_c.control ~ micro.phase_c_control

    # Current sensing
    power_stage.phase_a.current_sensor.output ~ micro.phase_a_current
    power_stage.phase_b.current_sensor.output ~ micro.phase_b_current
    power_stage.phase_c.current_sensor.output ~ micro.phase_c_current

    # Phase outputs
    ios.out_a ~ power_stage.phase_a.half_bridge.output
    ios.out_b ~ power_stage.phase_b.half_bridge.output
    ios.out_c ~ power_stage.phase_c.half_bridge.output

    # Position sensing
    position_sensor.spi ~ micro.spi

    # Connectors
    ios.i2c ~ micro.i2c
    ios.can ~ micro.can
    ios.usb2 ~ micro.usb2
    ios.power_usb ~ power_supply.power_usb
    ios.power_batt ~ power_supply.power_batt

    # Button and Addressable LED
    ios.button ~ micro.button
    ios.led ~ micro.led
