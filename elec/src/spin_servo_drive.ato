#ato view --root-file servo_drive.ato --root-node servo_drive.ato/Servo_Drive

import Resistor from "std/resistor.ato"

import GenericPowerStage from "generic_power_stage.ato"
import stm32_kit from "stm32.ato"
import BuckReg from "buck_reg.ato"
import LDOReg3V3 from "ldo_reg.ato"
import LDOReg5V from "ldo_reg.ato"

import Vdiv from "vdiv.ato"
import DMT3009LDT_7 from "DMT3009LDT_7.ato"
import VoltageSense from "voltage_sense.ato"
import PositionSensor from "position_sensor.ato"
import Usbc from "usbc.ato"
import DIODE_1N5819WS from "1N5819WS.ato"
import SK6805SIDE_S from "SK6805SIDE_S.ato"
import XT30 from "xt30.ato"
import NoButton from "button.ato"
import LTC4311 from "stemma.ato"
import JSTGH4Pin from "jst_gh.ato"
import indicator_led from "indicator_led.ato"

module SpinServoDrive:
    signal v_pwr
    signal v_gate
    signal v_5
    signal v_3
    signal gnd

    # Power input connector
    xt30 = new XT30
    v_pwr ~ xt30.vin
    gnd ~ xt30.gnd

    # Micro
    micro_kit = new stm32_kit
    micro_kit.VDD ~ v_3
    micro_kit.gnd ~ gnd

    # LED
    led1 = new SK6805SIDE_S
    led1.vcc ~ v_5 # connect to 5V rail
    led1.gnd ~ gnd
    led1.data_in ~ micro_kit.led_pixel
    led2 = new SK6805SIDE_S
    led2.vcc ~ v_5 # connect to 5V rail
    led2.gnd ~ gnd
    led2.data_in ~ led1.data_out

    indicator = new indicator_led
    indicator.input ~ micro_kit.led_pixel
    indicator.gnd ~ gnd

    # Enumeration switch
    switch = new NoButton
    switch_pulldown = new Resistor
    switch_pulldown.value = "10k"
    switch_pulldown.p1 ~ gnd
    switch_pulldown.p2 ~ switch.in
    switch.out ~ v_3
    switch.in ~ micro_kit.enum_sw

    # USB-C
    usb = new Usbc
    r_cc1 = new Resistor
    r_cc1.value = "5.1k"
    r_cc2 = new Resistor
    r_cc2.value = "5.1k"
    usb.dp ~ micro_kit.usb_dp
    usb.dm ~ micro_kit.usb_dm

    usb_diode = new DIODE_1N5819WS
    v_5 ~ usb_diode.cathode
    usb_diode.anode ~ usb.vbus

    # CC1 and CC2 so it works with USB C to USB C cables
    usb.cc1 ~ r_cc1.p1
    usb.cc2 ~ r_cc2.p1

    r_cc1.p2 ~ gnd
    r_cc2.p2 ~ gnd

    usb.gnd ~ gnd

    # Temperature sensors
    temp_sensor_fets = new Vdiv
    temp_sensor_fets.r_top.value = "10k"
    temp_sensor_fets.r_bt.value = "10k_NTC"
    temp_sensor_fets.input ~ v_3
    signal temp_sense ~ temp_sensor_fets.output
    temp_sensor_fets.output ~ micro_kit.temp_fet
    temp_sensor_fets.gnd ~ gnd

    # gate driver regulator
    gate_reg = new BuckReg
    gate_reg.vin ~ v_pwr
    gate_reg.vout ~ v_gate
    gate_reg.gnd ~ gnd

    # 5v rail LDO - powers micro
    ldo_5v = new LDOReg5V
    v_gate ~ ldo_5v.vin
    ldo_5v.vout ~ v_5
    ldo_5v.gnd ~ gnd

    # 3v3 rail LDO - powers micro
    ldo_3v3 = new LDOReg3V3
    ldo_3v3.vin ~ v_5
    ldo_3v3.vout ~ v_3
    ldo_3v3.gnd ~ gnd

    # power stage
    power_stage = new GenericPowerStage
    power_stage.vcc_pwr ~ v_pwr
    power_stage.vcc_gate ~ v_gate
    power_stage.vcc_3v3 ~ v_3
    power_stage.gnd ~ gnd

    # Power stage to micro
    micro_kit.ctrl_a_high ~ power_stage.ctrl_a.high
    micro_kit.ctrl_a_low ~ power_stage.ctrl_a.low
    micro_kit.ctrl_b_high ~ power_stage.ctrl_b.high
    micro_kit.ctrl_b_low ~ power_stage.ctrl_b.low
    micro_kit.ctrl_c_high ~ power_stage.ctrl_c.high
    micro_kit.ctrl_c_low ~ power_stage.ctrl_c.low

    # Todo: merge with low side current sense
    micro_kit.a_isense ~ power_stage.ctrl_a.i_fb
    micro_kit.b_isense ~ power_stage.ctrl_b.i_fb
    micro_kit.c_isense ~ power_stage.ctrl_c.i_fb

    # Voltage input monitoring
    voltage_input_vdiv = new VoltageSense
    voltage_input_vdiv.in ~ xt30.vin
    voltage_input_vdiv.gnd ~ gnd
    voltage_input_vdiv.out ~ micro_kit.vin_sense
    voltage_input_vdiv.c_filter.value = "100nF"

    # Position sensor - SPI interface
    position_sensor = new PositionSensor
    position_sensor.vin ~ v_3
    position_sensor.gnd ~ gnd
    micro_kit.spi_mosi ~ position_sensor.mosi
    micro_kit.spi_miso ~ position_sensor.miso
    micro_kit.spi_sck ~ position_sensor.sck
    micro_kit.spi_cs ~ position_sensor.cs


    # Following the PixHawk can pinout standard
    canbus_conn_1 = new JSTGH4Pin
    canbus_conn_1.p1 ~ v_3
    canbus_conn_1.p2 ~ micro_kit.CANH
    canbus_conn_1.p3 ~ micro_kit.CANL
    canbus_conn_1.p4 ~ gnd
    canbus_conn_2 = new JSTGH4Pin
    canbus_conn_2.p1 ~ v_3
    canbus_conn_2.p2 ~ micro_kit.CANH
    canbus_conn_2.p3 ~ micro_kit.CANL
    canbus_conn_2.p4 ~ gnd

